{- | \"Sieves\" by Iannis Xenakis and John Rahn
/Perspectives of New Music/
Vol. 28, No. 1 (Winter, 1990), pp. 58-78
-}
module Music.Theory.Xenakis.Sieve where

import qualified Data.List {- base -}

import qualified Music.Theory.List as List

-- | A Sieve.
data Sieve
  = -- | 'Empty' 'Sieve'
    Empty
  | -- | Primitive 'Sieve' of /modulo/ and /index/
    L (Integer, Integer)
  | -- | 'Union' of two 'Sieve's
    Union Sieve Sieve
  | -- | 'Intersection' of two 'Sieve's
    Intersection Sieve Sieve
  | -- | 'Complement' of a 'Sieve'
    Complement Sieve
  deriving (Eq, Show)

-- | The 'Union' of a list of 'Sieve's, ie. 'foldl1' 'Union'.
union :: [Sieve] -> Sieve
union = foldl1 Union

-- | The 'Intersection' of a list of 'Sieve's, ie. 'foldl1' 'Intersection'.
intersection :: [Sieve] -> Sieve
intersection = foldl1 Intersection

-- | Unicode synonym for 'Union'.
(âˆª) :: Sieve -> Sieve -> Sieve
(âˆª) = Union

-- | Unicode synonym for 'Intersection'.
(âˆ©) :: Sieve -> Sieve -> Sieve
(âˆ©) = Intersection

-- | Synonym for 'Complement'.
c :: Sieve -> Sieve
c = Complement

-- | Pretty-print sieve.  Fully parenthesised.
sieve_pp :: Sieve -> String
sieve_pp s =
  case s of
    Empty -> "âˆ…"
    L (p, q) -> concat [show p, ".", show q]
    Union p q -> concat ["(", sieve_pp p, " âˆª ", sieve_pp q, ")"]
    Intersection p q -> concat ["(", sieve_pp p, " âˆ© ", sieve_pp q, ")"]
    Complement p -> concat ["(âˆ ", sieve_pp p, ")"]

{- | Variant of 'L', ie. 'curry' 'L'.

> l 15 19 == L (15,19)
-}
l :: Integer -> Integer -> Sieve
l = curry L

-- | unicode synonym for 'l'.
(â‹„) :: Integer -> Integer -> Sieve
(â‹„) = l

infixl 3 âˆª
infixl 4 âˆ©
infixl 5 â‹„

{- | In a /normal/ 'Sieve' /m/ is '>' /i/.

>>> normalise (L (15,19))
L (15,4)

>>> normalise (L (11,13))
L (11,2)
-}
normalise :: Sieve -> Sieve
normalise s =
  case s of
    Empty -> Empty
    L (m, i) -> L (m, i `mod` m)
    Union s0 s1 -> Union (normalise s0) (normalise s1)
    Intersection s0 s1 -> Intersection (normalise s0) (normalise s1)
    Complement s' -> Complement (normalise s')

{- | Predicate to test if a 'Sieve' is /normal/.

>>> is_normal (L (15,4))
True

>>> is_normal (L (11,13))
False
-}
is_normal :: Sieve -> Bool
is_normal s = s == normalise s

{- | Predicate to determine if an 'I' is an element of the 'Sieve'.

>>> map (element (L (3,1))) [1..4]
[True,False,False,True]

>>> map (element (L (15,4))) [4,19 .. 49]
[True,True,True,True]
-}
element :: Sieve -> Integer -> Bool
element s n =
  case s of
    Empty -> False
    L (m, i) -> n `mod` m == i `mod` m && n >= i
    Union s0 s1 -> element s0 n || element s1 n
    Intersection s0 s1 -> element s0 n && element s1 n
    Complement s' -> not (element s' n)

{- | 'I' not in set.

>>> take 9 (i_complement [1,3..]) == [0,2..16]
True
-}
i_complement :: [Integer] -> [Integer]
i_complement =
  let f x s = case s of
        [] -> [x ..]
        e : s' -> case compare x e of
          LT -> x : f (x + 1) s
          EQ -> f (x + 1) s'
          GT -> error "i_complement"
  in f 0

{- | Construct the sequence defined by a 'Sieve'.  Note that building
     a sieve that contains an intersection clause that has no elements
     gives @_|_@.

>>> let d = [0,2,4,5,7,9,11]
>>> let r = d ++ map (+ 12) d
>>> take 14 (build (union (map (l 12) d))) == r
True
-}
build :: Sieve -> [Integer]
build s =
  let u_f = map List.head_err . Data.List.group
      i_f =
        let g [x, _] = [x]
            g _ = []
        in concatMap g . Data.List.group
  in case s of
      Empty -> []
      L (m, i) -> [i, i + m ..]
      Union s0 s1 -> u_f (List.merge (build s0) (build s1))
      Intersection s0 s1 -> i_f (List.merge (build s0) (build s1))
      Complement s' -> i_complement (build s')

{- | Variant of 'build' that gives the first /n/ places of the 'reduce' of 'Sieve'.

>>> buildn 6 (union (map (l 8) [0,3,6]))
[0,3,6,8,11,14]

>>> buildn 12 (L (3,2))
[2,5,8,11,14,17,20,23,26,29,32,35]

>>> buildn 9 (L (8,0))
[0,8,16,24,32,40,48,56,64]

>>> buildn 3 (L (3,2) âˆ© L (8,0))
[8,32,56]

>>> buildn 12 (L (3,1) âˆª L (4,0))
[0,1,4,7,8,10,12,13,16,19,20,22]

>>> buildn 14 (5â‹„4 âˆª 3â‹„2 âˆª 7â‹„3)
[2,3,4,5,8,9,10,11,14,17,19,20,23,24]

>>> buildn 6 (3â‹„0 âˆª 4â‹„0)
[0,3,4,6,8,9]

>>> buildn 8 (5â‹„2 âˆ© 2â‹„0 âˆª 7â‹„3)
[2,3,10,12,17,22,24,31]

>>> buildn 12 (5â‹„1 âˆª 7â‹„2)
[1,2,6,9,11,16,21,23,26,30,31,36]

>>> buildn 19 (L (3,2) âˆª L (7, 1))
[1,2,5,8,11,14,15,17,20,22,23,26,29,32,35,36,38,41,43]

>>> buildn 19 (3â‹„0 âˆª 7â‹„0)
[0,3,6,7,9,12,14,15,18,21,24,27,28,30,33,35,36,39,42]

>>> buildn 10 (3â‹„2 âˆ© 4â‹„7 âˆª 6â‹„9 âˆ© 15â‹„18)
[3,11,23,33,35,47,59,63,71,83]

>>> let s = 3â‹„2âˆ©4â‹„7âˆ©6â‹„11âˆ©8â‹„7 âˆª 6â‹„9âˆ©15â‹„18 âˆª 13â‹„5âˆ©8â‹„6âˆ©4â‹„2 âˆª 6â‹„9âˆ©15â‹„19
>>> let s' = 24â‹„23 âˆª 30â‹„3 âˆª 104â‹„70
>>> buildn 16 s == buildn 16 s'
True

>>> buildn 10 (24â‹„23 âˆª 30â‹„3 âˆª 104â‹„70)
[3,23,33,47,63,70,71,93,95,119]

>>> buildn 17 (5â‹„4 âˆª 3â‹„2 âˆª 7â‹„3)
[2,3,4,5,8,9,10,11,14,17,19,20,23,24,26,29,31]

>>> buildn 17 (5â‹„1 âˆª 3â‹„0 âˆª 7â‹„3)
[0,1,3,6,9,10,11,12,15,16,17,18,21,24,26,27,30]

>>> buildn 19 (5â‹„2 âˆª 3â‹„0 âˆª 7â‹„4)
[0,2,3,4,6,7,9,11,12,15,17,18,21,22,24,25,27,30,32]

Agon et. al. p.155

>>> let a = c (13â‹„3 âˆª 13â‹„5 âˆª 13â‹„7 âˆª 13â‹„9)
>>> let b = 11â‹„2
>>> let c' = c (11â‹„4 âˆª 11â‹„8)
>>> let d = 13â‹„9
>>> let e = 13â‹„0 âˆª 13â‹„1 âˆª 13â‹„6
>>> let f = (a âˆ© b) âˆª (c' âˆ© d) âˆª e
>>> buildn 13 f
[0,1,2,6,9,13,14,19,22,24,26,27,32]

>>> differentiate [0,1,2,6,9,13,14,19,22,24,26,27,32]
[1,1,4,3,4,1,5,3,2,2,1,5]

> import Music.Theory.Pitch
> let n = [0,1,2,6,9,13,14,19,22,24,26,27,32]
> let r = "C Cð„² Câ™¯ Dâ™¯ Eð„² Fð„° G Að„² B C Câ™¯ Cð„° E"
> unwords (map (pitch_class_pp . pc24et_to_pitch . (`mod` 24)) n) == r

Jonchaies

>>> let s = map (17â‹„) [0,1,4,5,7,11,12,16]
>>> let r = [1,3,1,2,4,1,4,1,1,3,1,2,4,1,4,1,1,3,1,2,4,1,4,1]
>>> differentiate (buildn 25 (union s)) == r
True

> let a2 = octpc_to_midi (2,9)
> let m = scanl (+) a2 r
> import Music.Theory.Pitch.Spelling.Table
> let p = "A2 A#2 C#3 D3 E3 G#3 A3 C#4 D4 D#4 F#4 G4 A4 C#5 D5 F#5 G5 G#5 B5 C6 D6 F#6 G6 B6 C7"
> unwords (map (pitch_pp_iso . midi_to_pitch pc_spell_sharp) m) == p

NekuÃ¯a

>>> let s = [24â‹„0,14â‹„2,22â‹„3,31â‹„4,28â‹„7,29â‹„9,19â‹„10,25â‹„13,24â‹„14,26â‹„17,23â‹„21,24â‹„10,30â‹„9,35â‹„17,29â‹„24,32â‹„25,30â‹„29,26â‹„21,30â‹„17,31â‹„16]
>>> let r = [2,1,1,3,2,1,3,1,2,1,4,3,1,4,1,4,1,3,1,4,1,3,1,4,1,4,1,1,3,1,3,1,2,3,1,4,1,4,4,1]
>>> differentiate (buildn 41 (union s)) == r
True

> let a0 = octpc_to_midi (0,9)
> let m = scanl (+) a0 r
> import Music.Theory.Pitch.Spelling.Table
> let p = "A0 B0 C1 C#1 E1 F#1 G1 A#1 B1 C#2 D2 F#2 A2 A#2 D3 D#3 G3 G#3 B3 C4 E4 F4 G#4 A4 C#5 D5 F#5 G5 G#5 B5 C6 D#6 E6 F#6 A6 A#6 D7 D#7 G7 B7 C8"
> unwords (map (pitch_pp_iso . midi_to_pitch pc_spell_sharp) m) == p

>>> let s = [8â‹„0âˆ©3â‹„0,2â‹„0âˆ©7â‹„2,2â‹„1âˆ©11â‹„3,31â‹„4,4â‹„3âˆ©7â‹„0,29â‹„9,19â‹„10,25â‹„13,8â‹„6âˆ©3â‹„2,2â‹„1âˆ©13â‹„4,23â‹„21,8â‹„2âˆ©3â‹„1,2â‹„1âˆ©3â‹„0âˆ©5â‹„4,5â‹„2âˆ©7â‹„3,29â‹„24,32â‹„25,2â‹„1âˆ©3â‹„2âˆ©5â‹„4,2â‹„1âˆ©13â‹„8,2â‹„1âˆ©3â‹„2âˆ©5â‹„2,31â‹„16]
>>> differentiate (buildn 41 (union s)) == r
True

Major scale:

>>> let s = (c(3â‹„2) âˆ© 4â‹„0) âˆª (c(3â‹„1) âˆ© 4â‹„1) âˆª (3â‹„2 âˆ© 4â‹„2) âˆª (c(3â‹„0) âˆ© 4â‹„3)
>>> buildn 7 s
[0,2,4,5,7,9,11]

Nomos Alpha:

>> let s = (c (13â‹„3 âˆª 13â‹„5 âˆª 13â‹„7 âˆª 13â‹„9) âˆ© 11â‹„2) âˆª (c (11â‹„4 âˆª 11â‹„8) âˆ© 13â‹„9) âˆª (13â‹„0 âˆª 13â‹„1 âˆª 13â‹„6)
>> buildn 32 s
[0,1,2,6,9,13,14,19,22,24,26,27,32,35,39,40,45,52,53,58,61,65,66,71,78,79,84,87,90,91,92,97]
-}
buildn :: Int -> Sieve -> [Integer]
buildn n = take n . build . reduce

{- | Standard differentiation function.

>>> differentiate [1,3,6,10]
[2,3,4]

>>> differentiate [0,2,4,5,7,9,11,12]
[2,2,1,2,2,2,1]
-}
differentiate :: (Num a) => [a] -> [a]
differentiate x = zipWith (-) (List.tail_err x) x

{- | Euclid's algorithm for computing the greatest common divisor.

>>> euclid 1989 867
51
-}
euclid :: (Integral a) => a -> a -> a
euclid i j =
  let k = i `mod` j
  in if k == 0 then j else euclid j k

{- | Bachet De MÃ©ziriac's algorithm.

>>> de_meziriac 15 4
3

>> euclid 15 4
1
-}
de_meziriac :: (Integral a) => a -> a -> a
de_meziriac i j =
  let f t =
        if (t * i) `mod` j /= 1
          then f (t + 1)
          else t
  in if j == 1 then 1 else f 1

{- | Attempt to reduce the 'Intersection' of two 'L' nodes to a singular 'L' node.

>>> reduce_intersection (3,2) (4,7)
Just (12,11)

>>> reduce_intersection (12,11) (6,11)
Just (12,11)

>>> reduce_intersection (12,11) (8,7)
Just (24,23)
-}
reduce_intersection :: (Integral t) => (t, t) -> (t, t) -> Maybe (t, t)
reduce_intersection (m1, i1) (m2, i2) =
  let d = euclid m1 m2
      i1' = i1 `mod` m1
      i2' = i2 `mod` m2
      c1 = m1 `div` d
      c2 = m2 `div` d
      m3 = d * c1 * c2
      t = de_meziriac c1 c2
      i3 = (i1' + t * (i2' - i1') * c1) `mod` m3
  in if d /= 1 && (i1' - i2') `mod` d /= 0
      then Nothing
      else Just (m3, i3)

{- | Reduce the number of nodes at a 'Sieve'.

>>> reduce (L (3,2) âˆª Empty)
L (3,2)

>>> reduce (L (3,2) âˆ© Empty)
L (3,2)

>>> reduce (L (3,2) âˆ© L (4,7))
L (12,11)

>>> reduce (L (6,9) âˆ© L (15,18))
L (30,3)

>>> let s = 3â‹„2âˆ©4â‹„7âˆ©6â‹„11âˆ©8â‹„7 âˆª 6â‹„9âˆ©15â‹„18 âˆª 13â‹„5âˆ©8â‹„6âˆ©4â‹„2 âˆª 6â‹„9âˆ©15â‹„19
>>> reduce s == (24â‹„23 âˆª 30â‹„3 âˆª 104â‹„70)
True

>>> sieve_pp (reduce s) == "((24.23 âˆª 30.3) âˆª 104.70)"
True

>>> let s = 3â‹„2âˆ©4â‹„7âˆ©6â‹„11âˆ©8â‹„7 âˆª 6â‹„9âˆ©15â‹„18 âˆª 13â‹„5âˆ©8â‹„6âˆ©4â‹„2 âˆª 6â‹„9âˆ©15â‹„19
>>> reduce s == (24â‹„23 âˆª 30â‹„3 âˆª 104â‹„70)
True
-}
reduce :: Sieve -> Sieve
reduce s =
  let f g s1 s2 =
        let s1' = reduce s1
            s2' = reduce s2
            s' = g s1' s2'
        in if s1 == s1' && s2 == s2'
            then s'
            else reduce s'
  in case s of
      Empty -> Empty
      L _ -> s
      Union s1 Empty -> s1
      Union s1 s2 -> f Union s1 s2
      Intersection s1 Empty -> s1
      Intersection (L p) (L q) -> maybe Empty L (reduce_intersection p q)
      Intersection s1 s2 -> f Intersection s1 s2
      Complement s' -> Complement (reduce s')

-- * Literature

psappha_flint_c :: [Sieve]
psappha_flint_c =
  let s0 = (8 â‹„ 0 âˆª 8 â‹„ 1 âˆª 8 â‹„ 7) âˆ© (5 â‹„ 1 âˆª 5 â‹„ 3)
      s1 = (8 â‹„ 0 âˆª 8 â‹„ 1 âˆª 8 â‹„ 2) âˆ© 5 â‹„ 0
      s2 = 8 â‹„ 3 âˆ© (5 â‹„ 0 âˆª 5 â‹„ 1 âˆª 5 â‹„ 2 âˆª 5 â‹„ 3 âˆª 5 â‹„ 4)
      s3 = 8 â‹„ 4 âˆ© (5 â‹„ 0 âˆª 5 â‹„ 1 âˆª 5 â‹„ 2 âˆª 5 â‹„ 3 âˆª 5 â‹„ 4)
      s4 = (8 â‹„ 5 âˆª 8 â‹„ 6) âˆ© (5 â‹„ 2 âˆª 5 â‹„ 3 âˆª 5 â‹„ 4)
      s5 = 8 â‹„ 1 âˆ© 5 â‹„ 2
      s6 = 8 â‹„ 6 âˆ© 5 â‹„ 1
  in [s0, s1, s2, s3, s4, s5, s6]

{- | /Psappha/ (Flint)

>>> buildn 27 psappha_flint
[0,1,3,4,6,8,10,11,12,13,14,16,17,19,20,22,23,25,27,28,29,31,33,35,36,37,38]
-}
psappha_flint :: Sieve
psappha_flint = union psappha_flint_c

a_r_squibbs_c :: [Sieve]
a_r_squibbs_c =
  [ 8 â‹„ 0 âˆ© (11 â‹„ 0 âˆª 11 â‹„ 4 âˆª 11 â‹„ 5 âˆª 11 â‹„ 6 âˆª 11 â‹„ 10)
  , 8 â‹„ 1 âˆ© (11 â‹„ 2 âˆª 11 â‹„ 3 âˆª 11 â‹„ 6 âˆª 11 â‹„ 7 âˆª 11 â‹„ 9)
  , 8 â‹„ 2 âˆ© (11 â‹„ 0 âˆª 11 â‹„ 1 âˆª 11 â‹„ 2 âˆª 11 â‹„ 3 âˆª 11 â‹„ 5 âˆª 11 â‹„ 10)
  , 8 â‹„ 3 âˆ© (11 â‹„ 1 âˆª 11 â‹„ 2 âˆª 11 â‹„ 3 âˆª 11 â‹„ 4 âˆª 11 â‹„ 10)
  , 8 â‹„ 4 âˆ© (11 â‹„ 0 âˆª 11 â‹„ 4 âˆª 11 â‹„ 8)
  , 8 â‹„ 5 âˆ© (11 â‹„ 0 âˆª 11 â‹„ 2 âˆª 11 â‹„ 3 âˆª 11 â‹„ 7 âˆª 11 â‹„ 9 âˆª 11 â‹„ 10)
  , 8 â‹„ 6 âˆ© (11 â‹„ 1 âˆª 11 â‹„ 3 âˆª 11 â‹„ 5 âˆª 11 â‹„ 7 âˆª 11 â‹„ 8 âˆª 11 â‹„ 9)
  , 8 â‹„ 7 âˆ© (11 â‹„ 1 âˆª 11 â‹„ 3 âˆª 11 â‹„ 6 âˆª 11 â‹„ 7 âˆª 11 â‹„ 8 âˆª 11 â‹„ 10)
  ]

{- | Ã€ R. (Hommage Ã  Maurice Ravel) (Squibbs, 1996)

>>> buildn 42 a_r_squibbs
[0,2,3,4,7,9,10,13,14,16,17,21,23,25,29,30,32,34,35,38,39,43,44,47,48,52,53,57,58,59,62,63,66,67,69,72,73,77,78,82,86,87]
-}
a_r_squibbs :: Sieve
a_r_squibbs = union a_r_squibbs_c
